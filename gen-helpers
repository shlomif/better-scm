#!/usr/bin/env perl

use strict;
use warnings;
use 5.014;
use autodie;

use Carp               ();
use File::Find::Object ();
use Path::Tiny         qw/ path /;

sub _run_cli_command
{
    my ( $cmd, $err ) = @_;

    if ( system(@$cmd) )
    {
        say "Running [@$cmd] failed!";
        Carp::confess($err);
    }
    return;
}

_run_cli_command( [ $^X, 'bin/my-cookiecutter.pl' ], 'cookiecutter failed.' );

_run_cli_command(
    [ "gmake", "--silent", "-f", "lib/make/build-deps/build-deps.mak", ],
    "build-deps failed!",
);

my $DIR = "lib/make/";

my @docs_paths;
my @dirs_paths;
my @images_paths;
my $host = "src";
my $tree = File::Find::Object->new( {}, $host );

FIND_FILES:
while ( my $r = $tree->next_obj() )
{
    my $path = join "/", @{ $r->full_components() };
    if ( $r->is_file() )
    {
        if ( $path =~ s#\.x?html\K\.tt2\z##ms )
        {
            push @docs_paths, $path;
        }
        elsif ( $path !~ m#(?:\~)\z#ms )
        {
            push @images_paths, $path;
        }
    }
    elsif ( $r->is_dir() )
    {
        if ( length($path) )
        {
            push @dirs_paths, $path;
        }
    }
}

path("${DIR}/include.mak")->spew_utf8(
    map {
        my ( $key, $paths ) = @{$_};
        join( " ", uc( "SCM" . "_" . $key ), ":=", sort @$paths ) . "\n"
    } [ "images", \@images_paths, ],
    [ "dirs", \@dirs_paths, ],
    [ "docs", \@docs_paths, ],
);

path("${DIR}/tt2.txt")
    ->spew_raw( join "", ( ( map { "$_\n" } sort @docs_paths ), ) );

path('Makefile')->spew_utf8("include ${DIR}/main.mak\n");
_run_cli_command( [ "gmake", 'bulk-make-dirs' ], "gmake failed" );
